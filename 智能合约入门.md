# 智能合约入门

## 简单的智能合约

让我们从一个最简单的例子开始。现在不需要你明白所有的细节，后面我们会慢慢深入的。

### 存储合约

```solidity
pragma solidity ^0.4.0;

contract SimpleStorage {
    uint storedData;

    function set(uint x) public {
        storedData = x;
    }

    function get() public constant returns (uint) {
        return storedData;
    }
}
```

第一行只是声明这份代码所适配的Solidity编译器版本：等于0.4.0或者大于0.4.0但是没有功能性兼容问题的版本（小于但不等于0.5.0）。这能保证合约不会在新的编译器上出现不同的行为。关键字`pragma`之所以用做这个用途是因为，一般情况下，`pragma`是用来告知编译器如何处理源码的指令（例如：[pragma once](https://en.wikipedia.org/wiki/Pragma_once)）。

Solidity语言里的合约是Ethereum区块链上特定地址里代码（她的函数）和数据（她的状态）的集合。这一行`uint storedData;`声明了一个`uint`类型的叫做`storedData`的状态变量（这里的`uint`类型表示无符号的长度为256位的整数）。你可以认为它是数据库里单独的一个槽，可以被数据库管理代码的函数查询和更改。在Ethereum里，这个变量在数据属于其所在的合约里。同样的，`set`和`get`函数可以用了修改或者获取这个变量的值。

为了访问状态变量，你不需要使用`this.`前缀，虽然其他语言一般情况下都需要这个前缀。

这个合约并没有做太多事情（因为Ethereum的基础设备问题），除了允许任何人存储一个可以被世界上所有的人都能访问的数字，而存储这个数字的人也没有（可行的）方法去阻止你来公布这个数字。当然，任何人也可以再次调用`set`来设置一个不同的数字，但是你的数字还是会被存储到区块链历史里。待会儿，我们将看到你能怎么增强访问限制来保证只有你才能更改这个数字。

> **Note**
> 
> 所有的标识符（合约名称，函数名和变量名）都必须是ASCII字符集。在字符串变量中可以存储UTF-8编码的数据。

---

> **Warning**
> 
> 使用Unicode字符时一定要小心，因为看起来类似（甚至一样）的字符可以有不同的编码值，所以会被编码为不同的字节数组。

### 子货币示例

接下来的合约将会实现一个最简单的加密货币。生产空气币不是不可能的，不过只有合约的创造者才可以（实现不同的发型方案是比较简单的）。另外，任何人之间都可以在不需要用户名和密码的情况下发送货币--唯一需要的只是Ethereum密码对。

```solidity
pragma solidity ^0.4.20; // should actually be 0.4.21

contract Coin {
    // The keyword "public" makes those variables
    // readable from outside.
    address public minter;
    mapping (address => uint) public balances;

    // Events allow light clients to react on
    // changes efficiently.
    event Sent(address from, address to, uint amount);

    // This is the constructor whose code is
    // run only when the contract is created.
    function Coin() public {
        minter = msg.sender;
    }

    function mint(address receiver, uint amount) public {
        if (msg.sender != minter) return;
        balances[receiver] += amount;
    }

    function send(address receiver, uint amount) public {
        if (balances[msg.sender] < amount) return;
        balances[msg.sender] -= amount;
        balances[receiver] += amount;
        emit Sent(msg.sender, receiver, amount);
    }
}
```

这个合约引入了一些新的概念，让我们一个一个的过一遍。

这一行`address public minter;`声明了一个公共的地址类型的状态变量。`address`类型是一个160位的不允许任何算术操作的值。它适合存储合约的地址或者外部参与者的密码对。关键字`public`自动创建一个函数，你可以通过这个函数在此合约外部访问被修饰的状态变量的当前值。没有这个关键字，其他的合约将无法访问此变量。有编译器自动生成的函数代码大致如下：

```solidity
function minter() returns (address) { return minter; }
```

当然，手动添加一个上面的函数是不能正常工作的，因为我们将会有一对拥有同样名字的函数和状态变量（*译者注:函数和状态变量不能同名?*）


